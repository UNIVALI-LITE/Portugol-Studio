//import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.internal.os.OperatingSystem

ext {
    
    mainClass = 'br.univali.ps.ui.Lancador'    
    splashScreen = 'splash.png'
}

dependencies {

    compile project(':portugol-core')
    
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    compile group: 'commons-codec', name: 'commons-codec', version: '1.6'
    
    // https://mvnrepository.com/artifact/org.apache.commons/commons-exec
    compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    
    // https://mvnrepository.com/artifact/commons-logging/commons-logging
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.3'
    
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/fluent-hc
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.3.3'
    
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3.3'
    
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient-cache
    compile group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: '4.3.3'
    
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpmime
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.3.3'
    
    // https://mvnrepository.com/artifact/org.jsoup/jsoup
    compile group: 'org.jsoup', name: 'jsoup', version: '1.7.2'

    // https://mvnrepository.com/artifact/com.fifesoft/autocomplete
    compile group: 'com.fifesoft', name: 'autocomplete', version: '2.5.4'
    
    // https://mvnrepository.com/artifact/com.fifesoft/rstaui
    compile group: 'com.fifesoft', name: 'rstaui', version: '2.5.4'
    
    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    compile group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '2.5.4'
    
    // https://mvnrepository.com/artifact/net.sf.cssbox/swingbox
    compile group: 'net.sf.cssbox', name: 'swingbox', version: '1.1'
    
    // https://mvnrepository.com/artifact/net.lingala.zip4j/zip4j
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.1'
    
    // https://mvnrepository.com/artifact/org.commonjava.googlecode.markdown4j/markdown4j
    compile group: 'org.commonjava.googlecode.markdown4j', name: 'markdown4j', version: '2.2-cj-1.0'
    
    // https://mvnrepository.com/artifact/org.json/json
    compile group: 'org.json', name: 'json', version: '20170516'

    testCompile group: 'org.assertj', name: 'assertj-swing-junit', version: '3.8.0'


    
    // Nenhuma das versões abaixo compila, então usamos a versão local
    //
    //    // https://mvnrepository.com/artifact/de.sciss/weblaf-core
    //    compile group: 'de.sciss', name: 'weblaf-core', version: '2.0.0'
    //    
    //    // https://mvnrepository.com/artifact/de.sciss/weblaf-ui
    //    compile group: 'de.sciss', name: 'weblaf-ui', version: '2.0.0'
    //
    //    // https://mvnrepository.com/artifact/com.weblookandfeel/weblaf-core
    //    compile group: 'com.weblookandfeel', name: 'weblaf-core', version: '1.2.8'
    //    
    //    // https://mvnrepository.com/artifact/com.weblookandfeel/weblaf-ui
    //    compile group: 'com.weblookandfeel', name: 'weblaf-ui', version: '1.2.8'    
}

task run(dependsOn: compileJava, type: JavaExec) {
    
    description = 'Runs this program as a JVM application'
	group = 'application'
	
    doFirst {
    
		main = mainClass
	    classpath = sourceSets.main.runtimeClasspath
	    jvmArgs = loadJvmArgs()
	    
	}
}

task debug(dependsOn: compileJava, type: JavaExec) {
    
    description = 'Runs this program as a JVM application for debugging'
	group = 'application'
	    
    doFirst {
    
	    main = mainClass
	    classpath = sourceSets.main.runtimeClasspath
	    jvmArgs = loadJvmArgs()
	    
	    debug = true
	}
}

def loadJvmArgs(def platform = null){

	def jvmConfigFile = new File(project.projectDir, 'jvm.config')
    def jvmargs = jvmConfigFile.readLines()
    
    def iterator = jvmargs.iterator()
    def osSpecificArguments = []
    
    while (iterator.hasNext()){
	
		def argument = iterator.next().trim()
		
		if (argument.isEmpty() || argument.startsWith('#') || argument.startsWith('//')){
		
			iterator.remove()
		}
		else if (argument.contains('->')) {
		
			def position = argument.lastIndexOf('->') + 2
			def osString = argument.substring(position, argument.length())			
			def osArray = osString.split(',')
			def osList = []
			
			
			for (os in osArray){
			
				osList.add(os.trim())
			}
			
			argument = argument.substring(0, argument.lastIndexOf('->')).trim()
			
			iterator.remove()
			
			if (platform == null){
				
				platform = [name: '']
				
				if (OperatingSystem.current().isWindows()){
					
					platform.name = 'windows'
				}
				else if (OperatingSystem.current().isMacOsX()){
					
					platform.name = 'osx'
				}
				else if (OperatingSystem.current().isLinux()){
					
					platform.name = 'linux'
				}
			}
			
			
			if (osList.contains(platform.name)){
				
				osSpecificArguments.add(argument)
			}
		}
	}
	
	jvmargs.addAll(osSpecificArguments)
    
    
    jvmargs.add('-splash:' + new File(project.projectDir, 'src/main/resources/' + splashScreen))
    
    if (OperatingSystem.current().isMacOsX()){
    
    	jvmargs.add('-Xdock:icon=' + new File(rootProject.projectDir, 'installer/arquivos/osx/icones/portugol-studio.icns').getPath())
    }
    
    return jvmargs
}

def loadJvmArgsForEclipse(){

	def jvmargs = loadJvmArgs()
	
	def iterator = jvmargs.iterator()
	
	while (iterator.hasNext()){
	
		def argument = iterator.next()
		
		if (argument.startsWith('-splash:') || argument.startsWith('-Xdock:icon')){
		
			iterator.remove()
		}
	}
	
	jvmargs.add('-splash:${workspace_loc:portugol-ide}/src/main/resources/' + splashScreen)
	
	if (OperatingSystem.current().isMacOsX()){
	
		jvmargs.add('-Xdock:icon=${workspace_loc:portugol-studio}/installer/arquivos/osx/icones/portugol-studio.icns')
	}
	
	return jvmargs.join(' ')
}

def createRunConfigurationsForEclipse(){

	def baseDir = new File(rootProject.projectDir, '.settings/launch-config')
	
	baseDir.mkdirs()
	
    def jvmargs = loadJvmArgsForEclipse()
	
	def studioRunConfig = 	
		'<?xml version="1.0" encoding="UTF-8" standalone="no"?>' +
		'	<launchConfiguration type="org.eclipse.jdt.launching.localJavaApplication">' + 
		'	<listAttribute key="org.eclipse.debug.ui.favoriteGroups">' +
		'		<listEntry value="org.eclipse.debug.ui.launchGroup.run"/>' +
        '		<listEntry value="org.eclipse.debug.ui.launchGroup.debug"/>' +
		'	</listAttribute>' +
		'	<booleanAttribute key="org.eclipse.jdt.launching.ATTR_USE_START_ON_FIRST_THREAD" value="true"/>' +
		'	<stringAttribute key="org.eclipse.jdt.launching.MAIN_TYPE" value="' + mainClass + '"/>' +
		'	<stringAttribute key="org.eclipse.jdt.launching.PROJECT_ATTR" value="portugol-ide"/>' +
		'	<stringAttribute key="org.eclipse.jdt.launching.VM_ARGUMENTS" value="' + jvmargs + '"/>' +
		'	<stringAttribute key="org.eclipse.jdt.launching.WORKING_DIRECTORY" value="${workspace_loc:portugol-ide}"/>'+
		'	</launchConfiguration>'
	
	def runConfigFile = new File(baseDir, 'Portugol Studio.launch')
	
	runConfigFile.createNewFile()
	runConfigFile.write(studioRunConfig)

    rootProject.tasks.findAll { task -> task.name.startsWith('dist') }.each({task -> 

        createGradleTaskConfigurationForEclipse(baseDir, task.name)
        
    })
}

def createGradleTaskConfigurationForEclipse(baseDir, taskName) {

    def configFile = new File(baseDir, 'Portugol Studio - Gradle [' + taskName + '].launch')
    def configContent = 
        '<?xml version="1.0" encoding="UTF-8" standalone="no"?>' +
        '<launchConfiguration type="org.eclipse.buildship.core.launch.runconfiguration">' +
        '   <listAttribute key="arguments"/>' +
        '   <stringAttribute key="gradle_distribution" value="GRADLE_DISTRIBUTION(WRAPPER)"/>' +
        '   <listAttribute key="jvm_arguments"/>' +
        '   <listAttribute key="org.eclipse.debug.ui.favoriteGroups">' +
        '       <listEntry value="org.eclipse.debug.ui.launchGroup.run"/>' +
        '   </listAttribute>' +
        '   <booleanAttribute key="show_console_view" value="true"/>' +
        '   <booleanAttribute key="show_execution_view" value="true"/>' +
        '   <listAttribute key="tasks">' +
        '       <listEntry value="' + taskName + '"/>' +
        '   </listAttribute>' +
        '   <booleanAttribute key="use_gradle_distribution_from_import" value="false"/>' +
        '   <stringAttribute key="working_dir" value="${workspace_loc:/portugol-studio}"/>' +
        '</launchConfiguration>'

        configFile.createNewFile()
        configFile.write(configContent)
}

createRunConfigurationsForEclipse()